%**********************************************************************************
%
%                CalC version 5.4.5, script "quoted.pde.par"
%                       Victor Matveev, May 23, 2005
%
%      Supplemental material for the Letter to the Editor of the Biophys. J.
%           "New and Corrected Simulations of Synaptic Facilitation"
%                    by V. Matveev, A. Sherman and R. Zucker
% 
%    This simulation script describes the PDE parameters quoted in Tang et al., 
%  pp. 2741-2743. We use the 20-nm geometry - see Fig. 5 ibid
%
%  This script is used by the main parent scripts "STF.growth.par",
% "STF.decay.stage1.par" and "STF.decay.stage2.par"
%__________________________________________________________________________________
%
%  Units are: micrometers for space, ms for time, microM for concentration,
%  and 1e-12 mole/ms for current (one can also use the conversion constant "pA" 
%  which is predefined for convenience)
%
%  Definitions appearing in a CalC script may be included in arbitrary order.
%==================================================================================
%                  1.  GEOMETRY  PARAMETERS  DEFINITIONS  (p. 2741)
%==================================================================================
 
% Synaptic bouton is represented as a cube of size 1.6 x 1.6 x 1 um (according to 
% p. 2741, the active zone lies in the center of a 2.56 um^2 space, which is a square
% with a side of 1.6 um). Since the active zone (AZ) is symmetric relative to the x- 
% and y-planes running through its center, we only have to consider a quarter of this
% area (we will have to impose zero flux/reflective boundary conditions for Ca2+ and
% buffers at the x- and y-planes - see below):

volume 0 0.8 0 0.8 0 1

a = 0.03 ; b = 0.07  % These are constant definitions used below
 
Ca.source a a 0   % These commands describe the channels array comprising the active 
Ca.source a b 0   % zone. We assume the geometry shown in Fig. 5 of Tang et al., 
Ca.source b a 0   % corresponding to 20nm resolution. Channel {x,y} coordinates are
Ca.source b b 0   % {30nm,30nm}, {30nm,70nm}, {70nm,30nm} and {70nm,70nm}.
 
% Next line defines the grid (discretization of space) for the numerical solver. 
% The number of points in the x- and y- directions is 34, and there are 40 points
% in the z-direction (the channels lye in the x-y plane; cf. Fig. 5 of Tang et al.). 
% These grid dimensions guarantee a numerical accuracy of about 5 % 

grid 34 34 40       

% To improve the spatial resolution, we use a non-uniform grid, with a higher
% density of grid points close to the active zone (spatial gradients are the
% largest in this region, so higher spatial resolution is required). The grid is
% smoothly stretched in all directions away from the active zone; for a
% given direction, each successive grid interval is given by a product of the 
% previous grid interval, and a factor slightly greater than one, defined by the 
% "stretch.factor" constant:

stretch.factor = 1.07  

% The following "stretch" commands describe the region of space where the grid
% points are dense. The grid is stretched in all 3 directions away from this region.
% In this case, the "dense" region is a 70 nm by 70 nm patch in the xy-plane,
% containing the 4 Ca2+ channels (see "Ca.source" definitions above)
 
stretch x 0 b  % Stretch in the x-direction starts from x = b = 0.07 um = 70 nm
stretch y 0 b  % Same for the grid stretch in the y-direction
stretch z 0 0  % In the z-direction stretching starts from z = c = 0 plane (membrane)

%==================================================================================
%                   2.  CALCIUM  PARAMETERS  DEFINITIONS  (p. 2742)
%==================================================================================
 
Ca.D   = 0.223 % The Ca2+ difussion coefficient (in units of um^2/ms)
Ca.bgr = 0.1   % Background Ca2+ concentration = 0.1 uM = 100 nM
 
% Now we define the boundary conditions (b.c.) for Ca2+, for each of the six 
% surfaces of the bouton volume: zero flux boundaries in x- and y- directions, and 
% pump b.c. on the two boundaries in z-direction:

Ca.bc Noflux Noflux Noflux Noflux Pump Pump

% While the zero flux b.c. is defined internally, the "Pump" b.c. we have to define 
% ourselves. A b.c. is defined by 3 constants (a, b and c) in the boundary equation
% "a dU/dn + b U = c", where U is the concentration field, and dU/dn is its
% derivative in the direction normal to the boundary. For pump rate P, we have
%
%            D dCa/dn - P (Ca - Ca.bgr) = 0   
%
% where D=0.223 um^2/ms is the diffusion coefficient and Ca.bgr=0.1 uM is the 
% steady-state Ca2+ concentration. Pump rate is P = 0.05 um/ms, so we define

bc.define Pump 1 -0.224 0  % defines the pump b.c. d[Ca]/dn - 0.224 ([Ca] - Ca.bgr) = 0
 
%==================================================================================
%                   3.  BUFFER  PARAMETERS  DEFINITIONS  (p. 2742)
%==================================================================================
 
buffer Bf           % This introduces the fixed buffer, and gives it a name "Bf"
Bf.D = 0.0          % Diffusion coefficient is zero, since it is fixed
Bf.total = 5760     % Total concentration is 5.76 mM
Bf.kplus = 0.1      % Binding rate 0.1 / (uM ms)
Bf.KD = 16          % Affinity is 16 uM
Bf.bc all Noflux    % Reflective (zero flux) boundary conditions on all surfaces
 
buffer Bm           % Let's call the mobile buffer "Bm"
Bm.D = 0.05         % Diffusion coefficient is 0.05 um^2/ms
Bm.total = 280      % Total concentration is 280 uM
Bm.kplus = 0.1      % Binding rate 0.1 / (uM ms)
Bm.KD = 2           % Affinity is 2 uM
Bm.bc all Noflux    % Reflective (zero flux) b.c.'s on all surfaces (this is the default)
 
% Fura-2 indicator dye properties:

% buffer F2         % Uncomment this line to include Fura-2
F2.D = 0.118        % Diffusion coefficient is 0.118 um^2/ms
F2.total = 400      % Total concentration is 400 uM
F2.kplus = 0.27     % Binding rate 0.1 / (uM ms)
F2.kminus = 0.0967  % Unbinding rate 0.0967 / ms
F2.bc all Noflux    % Reflective (zero flux) b.c.'s on all surfaces
 
%==================================================================================
%             4.  AUXILIARY  VARIABLE  AND  CONSTANT  DEFINITIONS
%==================================================================================

% Define variables tracking Ca2+ concentration at the secretory and STF sites:
 
CaX  :=  Ca[b,b,0.02]  % [Ca2+] at secretory sensor, 20 nm below corner channel
CaY  :=  Ca[b,b,0.10]  % [Ca2+] at facilitation sensor, 100 nm below corner channel
                       % at {x=70 nm, y=70 nm, z=0}

% Ca2+ current amplitudes during an action potential and in the tail:

I.AP   = 1.35  % Ca2+ current during an AP = 1.35e-21 mol/ms = 1.35e-9 fmol/us
I.tail = 4.60  % Ca2+ tail current = 4.6e-21 mol/ms = 4.6e-9 fmol/us
 

%==================================================================================
%                                  T H E   E N D  
%==================================================================================
